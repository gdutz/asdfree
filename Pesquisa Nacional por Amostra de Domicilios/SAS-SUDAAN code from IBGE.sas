LIBNAME PNAD11 "\\CHI00535081\Publico_IBGE\PNAD2011";


/* HOUSEHOLD VARIABLES */
DATA DOM2011(COMPRESS = YES);
	INFILE '\\CHI00535081\Publico_IBGE\PNAD2011\DOM2011.TXT' LRECL = 199 MISSOVER;
	INPUT 
      @00005  UF      $2.   /* UF                             */
      @00005  V0102   $8.   /* NUMERO DE CONTROLE             */
      @00013  V0103   $3.   /* NUMERO DE SERIE                */
      @00016  V0104   $2.   /* TIPO DE ENTREVISTA             */
      @00128  V4609    9.   /* PROJEÇÃO DE POPULAÇÃO          */
      @00137  V4610    3.   /* INVERSO DA FRAÇÃO              */
      @00140  V4611    5.   /* PESO DO DOMICÍLIO              */
      @00161  V4617    7.   /* STRAT - ID. AUTO E NAUT        */
      @00168  V4618    7.   /* PSU - UN. PRIMÁRIA ARAG        */;
run;


/* PERSON VARIABLES */
DATA PES2011(COMPRESS = YES);
	INFILE '\\CHI00535081\Publico_IBGE\PNAD2011\PES2011.TXT'  LRECL = 804 MISSOVER;
	INPUT
        @00005  UF      $2. /* UF                             */
        @00005  V0102   $8. /* NÚMERO DE CONTROLE             */
        @00013  V0103   $3. /* NÚMERO DE SÉRIE                */
        @00016  V0301   $2. /* NÚMERO DE ORDEM                */
        @00018  V0302    1. /* SEXO                           */
        @00027  V8005    3. /* IDADE DO MORADOR               */
        @00030  V0401   $1. /* CONDIÇÃO NA UNIDADE DOMICILIAR */
        @00751  V4729    5. /* PESO DA PESSOA                 */;
RUN;


/* ORDENAR ARQUIVO POR PÓS-ESTRATOS */
PROC SORT DATA = DOM2011;
	BY V4609;
RUN;


/* CREATE AN EXTERNAL FILE CONTAINING THE POSTSTRATIFICATION (PROJECTION FORECASTS) */
DATA _NULL_;
	SET DOM2011;
	FILE "\\CHI00535081\Publico_IBGE\PNAD2011\TOTAIS.TXT";
	BY V4609;
		IF FIRST.V4609 THEN PUT @1 V4609;
RUN;

/* SORT FILES FOR MERGE */
PROC SORT DATA = DOM2011;
	BY V0102 V0103;
PROC SORT DATA = DOM2011;
	BY V0102 V0103;
RUN;


/* MERGE FILES */
DATA PNAD2011 CRIT1 CRIT2;
	MERGE DOM2011(IN = A) PES2011(IN = B);
	BY V0102 V0103;
		IF A AND B THEN OUTPUT PNAD2011;
		ELSE IF A AND NOT B THEN OUTPUT CRIT1;
		ELSE IF B AND NOT A THEN OUTPUT CRIT2;
RUN;
/* THE FILE CRIT1 SHOULD CONTAIN THE REGISTERS OF THE HOUSEHOLDS THAT WERE NOT INTERVIEWED
   AND CRIT2 SHOULD BE EMPTY (BECAUSE ANY INTERVIEWED PEOPLE SHOULD BE IN SOME  INTERVIEWED HOUSEHOLD
   IN THE FILE PNAD2011). THE VARIABLE V0104 EXPLAINS THE REASON WHY THERE WAS NO INTERVIEW. */


/* SORT THE FILE BY STRATA AND PSU */
PROC SORT DATA = PNAD2011;
	BY V4617 V4609 V4618;
RUN;


/* ESTIMATE THE TOTAL OF PERSONS BY SEX USING SUDAAN */
PROC DESCRIPT DATA = PNAD2011 DESIGN = WR NOPRINT;
	VAR _ONE_;
	NEST V4617 V4618 / MISSUNIT;
	WEIGHT V4610;
	CLASS V0302 V4609;
	POSTVAR V4609;
	POSTWGT %INCLUDE "\\CHI00535081\Publico_IBGE\PNAD2011\TOTAIS.TXT";;
	TABLES V0302;
	OUTPUT TOTAL SETOTAL / FILENAME = SAIDA_SUDAAN FILETYPE = SAS REPLACE;
RUN;


/* ESTIMATES USING THE PNAD 2011 WEIGHTS PUBLISHED BY IBGE (ROUNDED POST-STRATIFIED) */
PROC MEANS DATA = PNAD2011 NOPRINT;
	CLASS V0302;
	VAR V4729;
	OUTPUT OUT = SAIDA_ARRED(DROP = _TYPE_ _FREQ_) SUM = TOTAL_ARRED;
RUN;


/* MERGE SEM BY */
DATA SAIDA_ARRED;
	SET SAIDA_ARRED;
		IF V0302 = . THEN V0302 = 0;
RUN;

DATA ESTIMATIVAS;
	MERGE SAIDA_SUDAAN(IN = A KEEP = V0302 TOTAL SETOTAL) SAIDA_ARRED(IN = B);
	BY V0302;
RUN;


/* COEFFICIENTS OF VARIATION */
DATA ESTIMATIVAS;
	SET ESTIMATIVAS;
		CV = 100*SETOTAL/TOTAL;
		CV_ARRED = 100*SETOTAL/TOTAL_ARRED;
RUN;


/* EXPORTING RESULTS */
PROC EXPORT DATA = ESTIMATIVAS OUTFILE = "\\CHI00535081\Publico_IBGE\PNAD2011\ESTIMATES.XLS" DBMS = XLS REPLACE;
RUN;
